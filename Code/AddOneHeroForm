Option Explicit


Private Sub btnDone_Click()

    Dim AddOneHeroForm As ufAddOneHeroForm
    Set AddOneHeroForm = Me

    If ErrorsPresent(AddOneHeroForm) = False Then
        Dim lngLastRow As Long
        
        With HeroCapabilities
            lngLastRow = GetLastRowOfSheetInColumn(HeroCapabilities, 1)
            
            Dim lngHeroColumn As Long
            lngHeroColumn = GetCellWithValueInRange(.Rows(1), "Hero", True).Column
            Dim lngColorColumn As Long
            lngColorColumn = GetCellWithValueInRange(.Rows(1), "Color", True).Column
            Dim lngPurpleRitualMaterialColumn As Long
            lngPurpleRitualMaterialColumn = GetCellWithValueInRange(.Rows(1), "Purple Ritual", True).Column
                            
            .Cells(lngLastRow + 1, lngHeroColumn).Value = AddOneHeroForm.textboxHeroName.Value
            .Cells(lngLastRow + 1, lngColorColumn).Value = GetHeroColor(AddOneHeroForm)
            
            If AddOneHeroForm.chkboxPurpleRitualMaterial = True Then
                .Cells(lngLastRow + 1, lngPurpleRitualMaterialColumn).Value = "Y"
            Else
                .Cells(lngLastRow + 1, lngPurpleRitualMaterialColumn).Value = "N"
            End If
        End With
        
        Me.Hide
        MsgBox "Hero Added."
    End If

End Sub


Private Function ErrorsPresent(ByVal AddOneHeroForm As ufAddOneHeroForm) As Boolean

    ErrorsPresent = False
    
    With AddOneHeroForm
        If .textboxHeroName.Value = vbNullString Then
            MsgBox "Must include Hero Name."
            ErrorsPresent = True
            Exit Function
        End If
    
        If .radioFire = False And .radioWater = False And .radioNature = False And .radioLight = False And .radioDark = False Then
            MsgBox "Must select Element."
            ErrorsPresent = True
            Exit Function
        End If
    End With

End Function


Private Function GetLastRowOfSheetInColumn(ByVal ShtInQuestion As Worksheet, ByVal lngColumnInQuestion As Long) As Long

    With ShtInQuestion
        'Using the end up method on a full sheet will go all the way to the top
        If .Cells(.Rows.Count, lngColumnInQuestion).Value <> vbNullString Then
            GetLastRowOfSheetInColumn = .Rows.Count
        Else
            GetLastRowOfSheetInColumn = .Cells(.Rows.Count, lngColumnInQuestion).End(xlUp).Row
        End If
    End With

End Function


Private Function GetCellWithValueInRange(ByVal rngToSearch As Range, ByVal strValueToFind As String, ByVal boolWholeValue As Boolean) As Range

    Dim rngFound As Range
    If boolWholeValue = True Then
        Set rngFound = rngToSearch.Find(What:=strValueToFind, Lookat:=xlWhole)
    Else
        Set rngFound = rngToSearch.Find(What:=strValueToFind, Lookat:=xlPart)
    End If

    If rngFound Is Nothing Then
        MsgBox "Can't find " & strValueToFind & " in " & rngToSearch.Address & " on the " & rngToSearch.Parent.Name & " sheet of the " & rngToSearch.Parent.Parent.Name & " workbook. This program cannot continue until it is present."
        'Cancel
    End If
    
    Set GetCellWithValueInRange = rngFound
    
End Function


Private Function GetHeroColor(ByVal AddOneHeroForm As ufAddOneHeroForm) As String

    Select Case True
        Case AddOneHeroForm.radioFire
            GetHeroColor = "Fire"
        Case AddOneHeroForm.radioWater
            GetHeroColor = "Water"
        Case AddOneHeroForm.radioNature
            GetHeroColor = "Nature"
        Case AddOneHeroForm.radioLight
            GetHeroColor = "Light"
        Case AddOneHeroForm.radioDark
            GetHeroColor = "Dark"
        Case Else
            GetHeroColor = 0
    End Select

End Function
